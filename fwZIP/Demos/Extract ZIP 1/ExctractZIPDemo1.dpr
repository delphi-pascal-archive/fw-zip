////////////////////////////////////////////////////////////////////////////////
//
//  ****************************************************************************
//  * Project   : FWZip
//  * Unit Name : ExctractZIPDemo1
//  * Purpose   : Демонстрация распаковки архива.
//  *           : Используется архив созданный демоприложением CreateZIPDemo1
//  * Author    : Александр (Rouse_) Багель
//  * Copyright : © Fangorn Wizards Lab 1998 - 2011.
//  * Version   : 1.0.1
//  * Home Page : http://rouse.drkb.ru
//  ****************************************************************************
//
//  Используемые источники:
//  ftp://ftp.info-zip.org/pub/infozip/doc/appnote-iz-latest.zip
//  http://zlib.net/zlib-1.2.5.tar.gz
//

// ВНИМАНИЕ!!!
// Данный код является демонстрационным и работает с данными по относительным путям.
// Если запускать код не из под отладчика, то относительный путь будет браться
// от расположения утилиты CMD.EXE что приведет к ошибке работы, ибо
// приозойдет попытка архивирования корневой директории.

// Данный пример показывает два варианта извлечения информации из архива.

program ExctractZIPDemo1;

{$APPTYPE CONSOLE}

uses
  Windows,
  Classes,
  SysUtils,
  FWZipReader;

var
  Zip: TFWZipReader;
  Index: Integer;
  S: TStringStream;
  OemString: AnsiString;
begin
  try
    Zip := TFWZipReader.Create;
    try
      // Открываем ранее созданный архив
      Zip.LoadFromFile('..\DemoResults\CreateZIPDemo1.zip');

      // Первый вариант распаковки - ручной доступ к каждому элементу архива

      // В примере CreateZIPDemo1 мы создали в корне архива файл Test.txt
      // Нам необходимо получить индекс этого элемента в архиве
      Index := Zip.GetElementIndex('test.txt');
      if Index >= 0 then
      begin
        // Распаковать можно в память:
        S := TStringStream.Create('');
        try
          Zip[Index].ExtractToStream(S, '');
          // Файл извлечен, выведем его содержимое в окно консоли
          OemString := AnsiString(S.DataString);
          AnsiToOem(PAnsiChar(OemString), PAnsiChar(OemString));
          Writeln(OemString);
        finally
          S.Free;
        end;

        // Распаковать так-же можно на диск:
        Zip[Index].Extract('..\DemoResults\CreateZIPDemo1\ManualExtract\', '');
      end;

      // Таким-же образом можно получить содержимое остальных файлов

      // Второй вариант распаковки - автоматической распаковка архива
      // в указанную папку на диске
      Zip.ExtractAll('..\DemoResults\CreateZIPDemo1\');
    finally
      Zip.Free;
    end;
  except
    on E: Exception do
      Writeln(E.ClassName, ': ', E.Message);
  end;
end.
